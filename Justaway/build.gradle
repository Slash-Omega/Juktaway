apply plugin: 'android'

dependencies {
    compile "com.android.support:support-v4:19.0.+"
    compile "com.google.code.gson:gson:2.2.4"
    compile "de.greenrobot:eventbus:2.2.0"
    def t4jVersion = "4.0.1"
    compile "org.twitter4j:twitter4j-core:${t4jVersion}"
    compile "org.twitter4j:twitter4j-async:${t4jVersion}"
    compile "org.twitter4j:twitter4j-media-support:${t4jVersion}"
    compile "org.twitter4j:twitter4j-stream:${t4jVersion}"
    compile "com.nostra13.universalimageloader:universal-image-loader:1.8.6"

    compile project(':plugins:ActionBar-PullToRefresh:library')
    compile fileTree(dir: 'libs', include: '*.jar')
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    sourceSets {
        // Move the tests to tests/java, tests/res, etc...
        androidTest.setRoot('tests')

        // Move the build types to build-types/<type>
        // For instance, build-types/debug/java, build-types/debug/AndroidManifest.xml, ...
        // This moves them out of them default location under src/<type>/... which would
        // conflict with src/ being used by the main source set.
        // Adding new build types or product flavors should be accompanied
        // by a similar customization.
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }

    signingConfigs {
        // debug {
        //     storeFile file("debug.keystore")
        // }

        release
    }

    buildTypes {
        release {
            runProguard false
            proguardFile getDefaultProguardFile('proguard-android.txt')
            proguardFile file('proguard-project.txt')

            if (project.hasProperty('storeFile')) {
                signingConfig signingConfigs.release
            }
        }
    }

    lintOptions {
        //checkReleaseBuilds false
        abortOnError false
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
    }
}

if (project.hasProperty('storeFile')) {
    android.signingConfigs.release.storeFile = file(storeFile)
}
if (project.hasProperty('storePassword')) {
    android.signingConfigs.release.storePassword = storePassword
}
if (project.hasProperty('keyAlias')) {
    android.signingConfigs.release.keyAlias = keyAlias
}
if (project.hasProperty('keyPassword')) {
    android.signingConfigs.release.keyPassword = keyPassword
}

